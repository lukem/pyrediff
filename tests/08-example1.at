AT_SETUP([example 1: simple example])

dnl Create pattern file.
dnl
AT_DATA([1.pattern], [dnl
First line
Second line with a date .*\.
])

dnl Create output file.
dnl
AT_DATA([1.output], [dnl
First line
Second line with a date 2014-11-22T16:41:00.
])

AT_CHECK([diff 1.pattern 1.output], [1], [dnl
2c2
< Second line with a date .*\.
---
> Second line with a date 2014-11-22T16:41:00.
])

AX_AT_DIFF_PATTERN([1.pattern], [1.output])

AX_AT_DIFF_PYRE([1.pattern], [1.output])

AT_CHECK([diff 1.pattern 1.output | ${AWK} -f ${abs_top_srcdir}/src/check_pattern.awk])

AT_CHECK([diff 1.pattern 1.output | ${PYTHON} ${abs_top_srcdir}/src/pyrediff -f])

AT_CHECK([${PYTHON} ${abs_top_srcdir}/src/pyrediff 1.pattern 1.output])

AT_CLEANUP()
