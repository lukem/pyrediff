AT_SETUP([example 2: extra lines in output])

dnl Create pattern file.
dnl
AT_DATA([2.pattern], [dnl
line 1 @<:@0-9@:>@+\.@<:@0-9@:>@+s
line 2
line 3
line 4
])

dnl Create output file.
dnl
AT_DATA([2.output], [dnl
line 1 25.63s
line 2
line 3
line 3b extra
line 4
])

dnl Define PYRE_09 for expected pattern / pyre output.
dnl
m4_pushdef([PYRE_09], [dnl
3a4
> line 3b extra
])dnl

AT_CHECK([diff 2.pattern 2.output], [1], [dnl
1c1
< line 1 @<:@0-9@:>@+\.@<:@0-9@:>@+s
---
> line 1 25.63s
3a4
> line 3b extra
])

AX_AT_DIFF_PATTERN([2.pattern], [2.output], [1], [PYRE_09])

AX_AT_DIFF_PYRE([2.pattern], [2.output], [1], [PYRE_09])

AT_CHECK([diff 2.pattern 2.output | ${AWK} -f ${abs_top_srcdir}/src/check_pattern.awk], [1], [PYRE_09])

AT_CHECK([diff 2.pattern 2.output | ${PYTHON} ${abs_top_srcdir}/src/pyrediff -f], [1], [PYRE_09])

AT_CHECK([${PYTHON} ${abs_top_srcdir}/src/pyrediff 2.pattern 2.output], [1], [PYRE_09])

m4_popdef([PYRE_09])dnl

AT_CLEANUP()
