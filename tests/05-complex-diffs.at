AT_SETUP([complex diffs])

dnl Create pattern file.
dnl
AT_DATA([05.re], [dnl
first line is ok
second line changes .*
second( second)? changes .*
third line
fourth line
fifth line
sixth line
])

dnl Create output file.
dnl
AT_DATA([05.out], [dnl
new first line
first line is ok
second line changes Sat Nov  9 08:23:01 EST 2013
second changes *
third line
fourth line change
fourth.2 new
fifth line
])

AT_CHECK([diff 05.re 05.out], [1], [dnl
0a1
> new first line
2,3c3,4
< second line changes .*
< second( second)? changes .*
---
> second line changes Sat Nov  9 08:23:01 EST 2013
> second changes *
5c6,7
< fourth line
---
> fourth line change
> fourth.2 new
7d8
< sixth line
])

dnl Define PYRE_05 for expected pattern / pyre output;
dnl diff with the 'second' regexes elided.
dnl
m4_pushdef([PYRE_05], [dnl
0a1
> new first line
5c6,7
< fourth line
---
> fourth line change
> fourth.2 new
7d8
< sixth line
])dnl

AT_CHECK([ax_at_diff_pattern 05.re 05.out], [1], [PYRE_05])

AT_CHECK([ax_at_diff_pyre 05.re 05.out], [1], [PYRE_05])

AX_AT_DIFF_PATTERN([05.re], [05.out], [1], [PYRE_05])

AX_AT_DIFF_PYRE([05.re], [05.out], [1], [PYRE_05])

m4_popdef([PYRE_05])dnl

AT_CLEANUP()
